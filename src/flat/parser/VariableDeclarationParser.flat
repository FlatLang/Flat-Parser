package flat/parser

import flat/ast/VariableDeclarationNode
import flat/compiler/models/Token

import flat/extensions/SyntaxStringFunctions

class extends ParserBase {
  override lazy TokenMatcher matcher => pattern.getMatcher()

  override lazy TokenPattern pattern => GroupTokenPattern([
    TokenPattern(Token.Type.IDENTIFIER, label: "declaration_type"),
    GroupTokenPattern([
      TokenPattern(Token.Type.SYMBOL, "[", label: "bracket_start"),
      TokenPattern(Token.Type.SYMBOL, "]", label: "bracket_end")
    ], repeat: true, optional: true),
    TokenPattern(Token.Type.IDENTIFIER, label: "declaration_name")
  ])

  override public checkStatementContinuation(ParseContext context, Token token) => token.type == Token.Type.SYMBOL

  override public generateNode(ParseContext context, TokenMatch match) -> VariableDeclarationNode {
    let type = match.getValue("declaration_type")
    let name = match.getValue("declaration_name")
    let bracketCount = match.getValues("bracket_start").count

    return VariableDeclarationNode(
      type: type,
      name: name,
      arrayDimensions: bracketCount
    )
  }
}