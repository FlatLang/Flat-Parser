package flat/parser/flat

import flat/parser
import flat/parser/matchers
import flat/ast/IfNode
import flat/compiler/models/Token
import flat/log/Logger

data class extends ParserBase implements ScopeableParser {
  static Logger log = Logger(IfParser.class)

  override lazy TokenPattern pattern => GroupTokenPattern([
    SingleTokenPattern(Token.Type.IDENTIFIER, "if"),
    NodeTokenPattern(conditionParsers, label: "condition"),
    NodeTokenPattern(LocalScopeParser().valueParsers, label: "if_body"),
  ], label: "if")

  ParserBase[] conditionParsers => [
    AnnotationParser(),
    NamedArgumentParser(),
    FunctionCallParser(),
    IdentifierParser(),
    LiteralParser(),
    RegexPatternParser(),
    LambdaParser(),
    ArrayInitializerParser(),
    UnaryOperationParser(),
    CastParser(),
    PriorityParser(),
    LocalDeclarationParser()
  ]

  override public checkStatementContinuation(ParseContext context, Token token) => true

  override public beginsScope() => true

  override public generateNode(ParseContext context, TokenMatch match = lastMatch) -> IfNode {
    return IfNode()
  }

  override public getChildNodeTokenMatches(ParseContext context, TokenMatch match) => NodeTokenMatch[] {
    if (let params = match.getMatch<GroupTokenMatch>("if")) {
      return (NodeTokenMatch[])params.tokenMatches.filter(m => m.label == "condition" || m.label == "if_body")
    }
  }
}