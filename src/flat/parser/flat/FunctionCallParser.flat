package flat/parser/flat

import flat/parser
import flat/parser/matchers
import flat/ast/FunctionCallNode
import flat/compiler/models/Token
import flat/compiler/models/SyntaxTokenFunctions

data class extends ParserBase {
  override ParserBase[] childParsers => Array():addAll([
    DotAccessParser(),
    SafeAccessParser(),
    IntRangeParser(),
    TernaryParser(),
    ColonAccessParser(),
    AssignmentParser(),
    ArrayAccessParser(),
    BinaryOperationParser(),
    UnaryOperationParser(),
    MatchCaseParser()
  ])

  override lazy TokenPattern pattern => GroupTokenPattern([
    CaptureTokenPattern(
      label: "generic_arguments",
      skipGenerics: false,
      start: Token(value: "<"),
      end: Token(value: ">"),
      allowedTokens: [
        Token(type: Token.Type.IDENTIFIER),
        Token(type: Token.Type.SYMBOL, value: "<"),
        Token(type: Token.Type.SYMBOL, value: ">"),
        Token(type: Token.Type.SYMBOL, value: "["),
        Token(type: Token.Type.SYMBOL, value: "]"),
        Token(type: Token.Type.SYMBOL, value: "("),
        Token(type: Token.Type.SYMBOL, value: ")"),
        Token(type: Token.Type.SYMBOL, value: "->"),
        Token(type: Token.Type.SYMBOL, value: "."),
        Token(type: Token.Type.SYMBOL, value: ",")
      ],
      optional: true
    ),
    IdentifierParser().pattern,
    SingleTokenPattern(type: Token.Type.SYMBOL, value: "("),
    GroupTokenPattern([
      NodeTokenPattern(argumentParsers, label: "argument"),
      GroupTokenPattern([
        SingleTokenPattern(type: Token.Type.SYMBOL, value: ","),
        NodeTokenPattern(argumentParsers, label: "argument")
      ], optional: true, repeat: true),
      SingleTokenPattern(type: Token.Type.SYMBOL, value: ",", optional: true),
    ], label: "arguments", optional: true),
    SingleTokenPattern(type: Token.Type.SYMBOL, value: ")")
  ])

  ParserBase[] argumentParsers => [
    AnnotationParser(),
    NamedArgumentParser(),
    FunctionCallParser(),
    IdentifierParser(),
    LiteralParser(),
    RegexPatternParser(),
    LambdaParser(),
    ArrayInitializerParser(),
    ArrayInstantiationParser(),
    UnaryOperationParser(),
    CastParser(),
    LocalDeclarationParser()
  ]

  override public generateNode() -> FunctionCallNode {
    return FunctionCallNode(
      name: lastMatch.getValue("name")
    )
  }

  override public getChildNodeTokenMatches() => NodeTokenMatch[] {
    if (let args = lastMatch.getMatch<GroupTokenMatch>("arguments")) {
      return (NodeTokenMatch[])args.tokenMatches.filter({ _.label == "argument" })
    }
  }
}