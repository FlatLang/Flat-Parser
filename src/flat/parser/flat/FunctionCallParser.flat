package flat/parser/flat

import flat/parser
import flat/parser/matchers
import flat/ast/FunctionCallNode
import flat/compiler/models/Token
import flat/compiler/models/SyntaxTokenFunctions

data class extends ParserBase {
  override ParserBase[] childParsers => Array():addAll([
    DotAccessParser(),
    ColonAccessParser(),
    AssignmentParser(),
    ArrayAccessParser(),
    BinaryOperationParser(),
    UnaryOperationParser(),
    TernaryParser(),
    MatchCaseParser()
  ])

  override lazy TokenPattern pattern => GroupTokenPattern([
    SingleTokenPattern(Token.Type.IDENTIFIER, label: "name", notValues: ["if"]),
    CaptureTokenPattern(
      label: "generic_arguments",
      start: Token(value: "<"),
      end: Token(value: ">"),
      allowedTokens: [
        Token(type: Token.Type.IDENTIFIER),
        Token(type: Token.Type.SYMBOL, value: "<"),
        Token(type: Token.Type.SYMBOL, value: ">"),
        Token(type: Token.Type.SYMBOL, value: "["),
        Token(type: Token.Type.SYMBOL, value: "]"),
        Token(type: Token.Type.SYMBOL, value: ",")
      ],
      optional: true
    ),
    SingleTokenPattern(Token.Type.SYMBOL, "("),
    GroupTokenPattern([
      NodeTokenPattern(argumentParsers, label: "argument"),
      GroupTokenPattern([
        SingleTokenPattern(Token.Type.SYMBOL, ","),
        NodeTokenPattern(argumentParsers, label: "argument")
      ], optional: true, repeat: true)
    ], label: "arguments", optional: true),
    SingleTokenPattern(Token.Type.SYMBOL, ")")
  ])

  ParserBase[] argumentParsers => [
    AnnotationParser(),
    NamedArgumentParser(),
    FunctionCallParser(),
    IdentifierParser(),
    LiteralParser(),
    RegexPatternParser(),
    LambdaParser(),
    ArrayInitializerParser(),
    UnaryOperationParser(),
    CastParser(),
    LocalDeclarationParser()
  ]

  override public generateNode() -> FunctionCallNode {
    return FunctionCallNode(
      name: lastMatch.getValue("name")
    )
  }

  override public getChildNodeTokenMatches() => NodeTokenMatch[] {
    if (let args = lastMatch.getMatch<GroupTokenMatch>("arguments")) {
      return (NodeTokenMatch[])args.tokenMatches.filter({ _.label == "argument" })
    }
  }
}