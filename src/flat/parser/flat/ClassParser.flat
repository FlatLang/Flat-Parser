package flat/parser/flat

import flat/parser
import flat/parser/matchers
import flat/ast
import flat/compiler/models/Token

data class extends ParserBase {
  override lazy TokenPattern pattern => GroupTokenPattern<ClassNode>([
    SingleTokenPattern(type: Token.Type.IDENTIFIER, values: ["class", "trait", "interface"], field: "type"),
    GroupTokenPattern([
      SingleTokenPattern<IdentifierNode>(type: Token.Type.IDENTIFIER, label: "class_name", field: "name"),
      NodeTokenPattern([GenericParametersParser()], optional: true)
    ], optional: true, greedy: false),
    GroupTokenPattern([
      SingleTokenPattern(type: Token.Type.IDENTIFIER, value: "extends"),
      SingleTokenPattern<IdentifierNode>(type: Token.Type.IDENTIFIER, label: "name", field: "name"),
      NodeTokenPattern([GenericArgumentsParser()], optional: true)
    ], optional: true, label: "extends_class"),
    GroupTokenPattern([
      SingleTokenPattern(type: Token.Type.IDENTIFIER, value: "implements"),
      SingleTokenPattern<IdentifierNode>(type: Token.Type.IDENTIFIER, label: "implements_class_name", field: "name"),
      NodeTokenPattern([GenericArgumentsParser()], optional: true),
      GroupTokenPattern([
        SingleTokenPattern(type: Token.Type.SYMBOL, value: ","),
        SingleTokenPattern<IdentifierNode>(type: Token.Type.IDENTIFIER, label: "implements_class_name", field: "name"),
        NodeTokenPattern([GenericArgumentsParser()], optional: true)
      ], repeat: true, optional: true, label: "implements_class"),
      SingleTokenPattern(type: Token.Type.SYMBOL, value: ",", optional: true),
    ], optional: true),
    GroupTokenPattern([
      SingleTokenPattern(type: Token.Type.SYMBOL, value: "{"),
      NodeGroupTokenPattern(classBodyParsers, optional: true, label: "class_body_contents"),
      SingleTokenPattern(type: Token.Type.SYMBOL, value: "}")
    ], label: "class_body")
  ])

  ParserBase[] classBodyParsers => [
    AnnotationParser(),
    ClassParser(),
    StaticBlockParser(),
    FieldParser(),
    ArrayOverrideParser(),
    FunctionParser(),
  ]
}