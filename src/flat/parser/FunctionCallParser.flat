package flat/parser

import flat/ast/FunctionCallNode
import flat/compiler/models/Token

class extends ParserBase {
  override ParserBase[] childParsers => Array():addAll([
    DotAccessParser(),
    ColonAccessParser()
  ])

  override let lazy TokenMatcher matcher => pattern.getMatcher()

  override let lazy TokenPattern pattern => GroupTokenPattern([
    TokenPattern(Token.Type.IDENTIFIER, label: "name"),
    CaptureTokenPattern(
      label: "generic_arguments",
      start: Token(value: "<"),
      end: Token(value: ">"),
      optional: true
    ),
    CaptureTokenPattern(
      label: "arguments",
      start: Token(value: "("),
      end: Token(value: ")")
    )
  ])

  lazy ParserBase[] argumentParsers => [
    AnnotationParser(),
    CommentParser(),
    FunctionCallParser(),
    IdentifierParser(),
    LiteralParser()
  ]

  override public checkStatementContinuation(ParseContext context, Token token) => token.type == Token.Type.SYMBOL

  override public generateNode(ParseContext context, TokenMatch match) -> FunctionCallNode {
    let name = match.getValue("name")

    return FunctionCallNode(
      name: name
    )
  }

  override public getChildParseRequests(ParseContext context, TokenMatch match) -> ParseRequest {
    Token[][] tokens = match
        .getMatch<CaptureTokenMatch>("arguments")
        .capture
        .split({ _.value == "," })

    return ParseRequest(
      requests: tokens.map({
        return ParseRequestData(
          tokens: _,
          parsers: argumentParsers
        )
      })
    )
  }
}