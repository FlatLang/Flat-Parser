package flat/parser

import flat/ast/ClassNode
import flat/compiler/models/Token

class extends ParserBase {
  override ParserBase[] childParsers => Array():addAll([
    ClassScopeParser()
  ])

  override let lazy TokenMatcher matcher => pattern.getMatcher()

  override let lazy TokenPattern pattern => GroupTokenPattern([
    TokenPattern(Token.Type.IDENTIFIER, "class"),
    TokenPattern(Token.Type.IDENTIFIER, label: "class_name", optional: true),
    GroupTokenPattern([
      TokenPattern(Token.Type.IDENTIFIER, "extends"),
      TokenPattern(Token.Type.IDENTIFIER, label: "extends_class_name")
    ], optional: true),
    GroupTokenPattern([
      TokenPattern(Token.Type.IDENTIFIER, "implements"),
      TokenPattern(Token.Type.IDENTIFIER, label: "implements_class_name"),
      GroupTokenPattern([
        TokenPattern(Token.Type.SYMBOL, ","),
        TokenPattern(Token.Type.IDENTIFIER, label: "implements_class_name")
      ], repeat: true, optional: true)
    ], optional: true)
  ])

  override public checkStatementContinuation(ParseContext context, Token token) => true

  override public generateNode(ParseContext context, TokenMatch match) -> ClassNode {
    let className = match.getValue("class_name")
    let extendedClassName = match.getValue("extends_class_name")
    let implementsClassNames = match.getValues("implements_class_name")

    return ClassNode(
      name: className ?: context.fileNode.file.rootName,
      implicitName: className == null
    )
  }
}