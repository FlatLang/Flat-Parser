package flat/parser

import flat/ast/FunctionNode
import flat/compiler/models/Token
import flat/log/Logger

class extends ParserBase {
  let static Logger log = Logger(FunctionParser.class)

  override ParserBase[] childParsers => Array():addAll([
    FunctionScopeParser()
  ])

  override public checkStatementContinuation(ParseContext context, Token token) => true

  override lazy TokenMatcher matcher => pattern.getMatcher()

  override lazy TokenPattern pattern => GroupTokenPattern([
    TokenPattern(Token.Type.IDENTIFIER, label: "name"),
    CaptureTokenPattern(
      label: "parameters",
      start: Token(value: "("),
      end: Token(value: ")")
    ),
    GroupTokenPattern([
      TokenPattern(Token.Type.SYMBOL, "->"),
      TokenPattern(Token.Type.IDENTIFIER, label: "type"),
      GroupTokenPattern([
        TokenPattern(Token.Type.SYMBOL, "[", label: "type_bracket_start"),
        TokenPattern(Token.Type.SYMBOL, "]", label: "type_bracket_end")
      ], repeat: true, optional: true)
    ], optional: true),
    GroupTokenPattern([
      TokenPattern(Token.Type.SYMBOL, "=>")
    ], optional: true)
  ])

  ParserBase[] parameterParsers => [ParameterParser()]

  override public generateNode(ParseContext context, TokenMatch match) -> FunctionNode {
    let type = match.getValue("type")
    let name = match.getValue("name")
    let bracketCount = match.getValues("bracket_start").count

    return FunctionNode(
      type: type,
      name: name,
      arrayDimensions: bracketCount
    )
  }

  override public getChildParseRequests(ParseContext context, TokenMatch match) -> ParseRequest {
    Token[][] tokens = match
        .getMatch<CaptureTokenMatch>("parameters")
        .capture
        .split({ _.value == "," })

    return ParseRequest(
      requests: tokens.map({
        return ParseRequestData(
          tokens: _,
          parsers: parameterParsers
        )
      })
    )
  }
}